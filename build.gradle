plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'checkstyle'
	id 'jacoco'
}

group = 'com.api'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
	java {
		googleJavaFormat()
	}
}

checkstyle {
	toolVersion = '10.12.7' // Or any recent version
	configFile = file('config/checkstyle/checkstyle.xml')
}

tasks.named('check') {
	dependsOn spotlessCheck, checkstyleMain, checkstyleTest
}

jacoco {
	toolVersion = '0.8.12'
}

tasks.named('jacocoTestReport') {
	reports {
		xml.required.set(true)
		csv.required.set(true)
		html.required.set(true)
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}
